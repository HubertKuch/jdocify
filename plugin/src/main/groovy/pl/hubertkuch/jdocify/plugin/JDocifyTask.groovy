package pl.hubertkuch.jdocify.plugin

import org.gradle.api.DefaultTask
import org.gradle.api.GradleException
import org.gradle.api.file.RegularFileProperty
import org.gradle.api.plugins.JavaPluginExtension
import org.gradle.api.provider.Property
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.InputFile
import org.gradle.api.tasks.Optional
import org.gradle.api.tasks.PathSensitive
import org.gradle.api.tasks.PathSensitivity
import org.gradle.api.tasks.TaskAction
import org.gradle.process.ExecOperations

import javax.inject.Inject
import java.nio.file.Files
import java.nio.file.Path

abstract class JDocifyTask extends DefaultTask {

    @Input
    @Optional
    abstract Property<String> getPackageToScan()

    @InputFile
    @Optional
    @PathSensitive(PathSensitivity.RELATIVE)
    abstract RegularFileProperty getSourcePropertiesFile()

    @Inject
    abstract ExecOperations getExecOperations()

    @TaskAction
    void run() {
        def finalScanPackage = packageToScan.getOrNull()

        if (finalScanPackage == null || finalScanPackage.isEmpty()) {
            throw new GradleException("Could not determine package to scan. Please set 'group' in your build.gradle or set 'jdocify.scanPackage' directly.")
        }

        def importProperties = new Properties()

        def propsFile = sourcePropertiesFile.get().asFile
        if (propsFile.exists()) {
            logger.lifecycle("Loading JDocify properties from: ${propsFile.path}")
            try (def fis = new FileInputStream(propsFile)) {
                importProperties.load(fis)
            } catch (IOException e) {
                throw new GradleException("Failed to load jdocify.properties from ${propsFile.path}", e)
            }
        } else {
            logger.info("No custom jdocify.properties found at: ${propsFile.path}. Using defaults.")
        }

        importProperties.setProperty("jdocify.scanPackage", finalScanPackage)

        def buildDir = project.layout.buildDirectory.get()
        def tempPropsFile = buildDir.file("jdocify/config.properties").asFile

        tempPropsFile.parentFile.mkdirs()

        try (def fos = new FileOutputStream(tempPropsFile)) {
            importProperties.store(fos, "Generated by JDocify Gradle plugin")
        }

        logger.lifecycle("Wrote temporary config for JDocify.main to: ${tempPropsFile.path}")

        def sourceSets = project.extensions.getByType(JavaPluginExtension).sourceSets
        def runtimeClasspath = sourceSets.main.runtimeClasspath

        if (runtimeClasspath.isEmpty()) {
            throw new GradleException("Could not find project runtime classpath. Is the 'java' plugin applied?")
        }

        logger.lifecycle("Executing pl.hubertkuch.jdocify.JDocify.main...")

        getExecOperations().javaexec {
            mainClass.set('pl.hubertkuch.jdocify.JDocify')
            classpath = runtimeClasspath
            systemProperties['jdocify.configFile'] = tempPropsFile.absolutePath
        }

        logger.lifecycle("JDocify execution finished.")
    }
}